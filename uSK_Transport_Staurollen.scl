FUNCTION_BLOCK "uSK_Transport_Staurollen"
{ S7_Optimized_Access := 'TRUE' }
VERSION : 0.1
   VAR_INPUT 
      I_szName { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : String[60];
      I_xKonfigMB { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : "stKonfigMB";
      I_xEcoModeAktiv { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : Bool;
      I_wEcoTime { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : DInt;
      I_xEcoTrigger1 { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : Bool;
      I_xEcoTrigger2 { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : Bool;
   END_VAR

   VAR_OUTPUT 
      O_xMotor_Freigabe1 { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : Bool;
      O_xMotor_Vor1 { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : Bool;   // Umrichter Rechtslauf
      O_xMotor_Rück1 { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : Bool;   // Umrichter Linkslauf
      O_slSollwert1 { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : DInt;   // Umrichter Sollwertvorgabe Geschwindigkeit
      O_xMotor_Freigabe2 { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : Bool;
      O_xMotor_Vor2 { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : Bool;   // Umrichter Rechtslauf
      O_xMotor_Rück2 { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : Bool;   // Umrichter Linkslauf
      O_slSollwert2 { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : DInt;   // Umrichter Sollwertvorgabe Geschwindigkeit
   END_VAR

   VAR_IN_OUT 
      IO_stVisuHandTransport1 : "stVisuHandTransport";   // HMI Schnittstelle
      IO_stVisuHandTransport2 : "stVisuHandTransport";   // HMI Schnittstelle
   END_VAR

   VAR 
      stSK { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'; S7_SetPoint := 'False'} : "stSKMerker";
      hSKInit { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'; S7_SetPoint := 'False'} : "Flanke";   // positive Flanke xIstNotHalt
      xFreigabe1 { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : Bool;
      xFreigabe2 { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : Bool;
      bGeschwindigkeit1 { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : DInt;   // Soll Geschwindigkeitsvorgabe
      bGeschwindigkeit2 { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : DInt;   // Soll Geschwindigkeitsvorgabe
      tAusschaltverzögerung1 {InstructionName := 'TOF_TIME'; LibVersion := '1.0'; ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'; S7_SetPoint := 'False'} : TOF_TIME;
      tAusschaltverzögerung2 {InstructionName := 'TOF_TIME'; LibVersion := '1.0'; ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'; S7_SetPoint := 'False'} : TOF_TIME;
      fGeschwindigkeit { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : Real;
   END_VAR


BEGIN
	//----------------------------------------------------------------------------------------------------------------------------------
	REGION Header
	    // ==================================================================
	    // Copyright ERSA GmbH
	    // 97877 Wertheim - Germany
	    // ------------------------------------------------------------------
	    // B E S C H R E I B U N G
	    // ------------------------------------------------------------------
	    // Transport Staurollenband
	    // ==================================================================
	END_REGION
	//----------------------------------------------------------------------------------------------------------------------------------
	REGION VISU  
	    //---------------------------------------------------------------------
	    // HMI
	    //---------------------------------------------------------------------
	    //Anzeige Motorzustand auf der HMI
	    #IO_stVisuHandTransport1.xBandSteht := (NOT #O_xMotor_Rück1 AND NOT #O_xMotor_Vor1);
	    #IO_stVisuHandTransport1.xAusgangVor := (NOT #O_xMotor_Rück1 AND #O_xMotor_Vor1);
	    #IO_stVisuHandTransport1.xAusgangRück := (#O_xMotor_Rück1 AND NOT #O_xMotor_Vor1);
	    
	    IF (#I_xKonfigMB.xTypGetrennterTransport = TRUE) THEN
	        #IO_stVisuHandTransport2.xBandSteht := (NOT #O_xMotor_Rück2 AND NOT #O_xMotor_Vor2);
	        #IO_stVisuHandTransport2.xAusgangVor := (NOT #O_xMotor_Rück2 AND #O_xMotor_Vor2);
	        #IO_stVisuHandTransport2.xAusgangRück := (#O_xMotor_Rück2 AND NOT #O_xMotor_Vor2);
	    END_IF;
	END_REGION
	//----------------------------------------------------------------------------------------------------------------------------------
	REGION Handling EcoMode    
	    // Zeit wird über das HMI in Minuten vorgegeben 
	    // Umrechnung von Minute in Millisekunden für den Timer ( ZeitHMI * 60000)
	    //---------------------------------------------------------------------
	    //---------------------------------------------------------------------
	    #tAusschaltverzögerung1(IN := #I_xEcoTrigger1,
	                           PT := DINT_TO_TIME(#I_wEcoTime * 60000));
	    //---------------------------------------------------------------------
	    IF ("DB_BA".stBetriebsart.xBetriebsfreigabe = TRUE) THEN
	        
	        IF (#I_xEcoModeAktiv = TRUE) THEN
	            #xFreigabe1 := #tAusschaltverzögerung1.Q;
	        ELSE
	            RESET_TIMER(TIMER := #tAusschaltverzögerung1);
	            #xFreigabe1 := TRUE;
	        END_IF;
	    ELSE
	        RESET_TIMER(TIMER := #tAusschaltverzögerung1);
	        #xFreigabe1 := FALSE;
	    END_IF;
	    //---------------------------------------------------------------------
	    //---------------------------------------------------------------------
	    IF (#I_xKonfigMB.xTypGetrennterTransport = TRUE) THEN
	        #tAusschaltverzögerung2(IN := #I_xEcoTrigger2,
	                                PT := DINT_TO_TIME(#I_wEcoTime * 60000));
	        //---------------------------------------------------------------------
	        IF ("DB_BA".stBetriebsart.xBetriebsfreigabe = TRUE) THEN
	            
	            IF (#I_xEcoModeAktiv = TRUE) THEN
	                #xFreigabe2 := #tAusschaltverzögerung2.Q;
	            ELSE
	                RESET_TIMER(TIMER := #tAusschaltverzögerung2);
	                #xFreigabe2 := TRUE;
	            END_IF;
	        ELSE
	            RESET_TIMER(TIMER := #tAusschaltverzögerung2);
	            #xFreigabe2 := FALSE;
	        END_IF;
	    END_IF;
	END_REGION
	//----------------------------------------------------------------------------------------------------------------------------------
	REGION INIT
	    #hSKInit(I_xVar := "DB_BA".stBetriebsart.xGrundstellung_Laeuft OR "DB_HMI_BAInfo".stTasten.xTasteAnwahlGrundstellung OR "DB_BA".stBetriebsart.xEinrichten);
	    //=====================================================================
	    // Schrittketten - Reset bei Grundstellungsfahrt
	    //=====================================================================
	    IF (#hSKInit.O_xFp) THEN
	        // SKs rücksetzen
	        IF "DB_BA".stBetriebsart.xEinrichten THEN
	            #stSK.#swSchrittnummer := 200;
	        ELSE
	            #stSK.#swSchrittnummer := 0;
	        END_IF;
	        #O_xMotor_Rück1 := FALSE;
	        #O_xMotor_Vor1 := FALSE;
	        #O_xMotor_Freigabe1 := FALSE;
	        #IO_stVisuHandTransport1.xRück := FALSE;
	        #IO_stVisuHandTransport1.xVor := FALSE;
	        #IO_stVisuHandTransport1.xStopp := FALSE;
	        
	        #O_xMotor_Rück2 := FALSE;
	        #O_xMotor_Vor2 := FALSE;
	        #O_xMotor_Freigabe2 := FALSE;
	        #IO_stVisuHandTransport2.xRück := FALSE;
	        #IO_stVisuHandTransport2.xVor := FALSE;
	        #IO_stVisuHandTransport2.xStopp := FALSE;
	    END_IF;
	END_REGION
	//----------------------------------------------------------------------------------------------------------------------------------
	REGION Steuerungsinfos
	    //=====================================================================
	    // SKx - Steuerungsinformationen
	    //=====================================================================
	    //---------------------------------------------------------------------
	    // Auswertung Schrittwechsel
	    //---------------------------------------------------------------------
	    IF #stSK.#swSchrittnummer <> #stSK.#swLetzteSchrittnummer THEN
	        RESET_TIMER(#stSK.#hSchrittzeit);
	    END_IF;
	    #stSK.#swLetzteSchrittnummer := #stSK.#swSchrittnummer;
	    //---------------------------------------------------------------------
	    //Laufzeiterfassung Schrittzeiten
	    //---------------------------------------------------------------------
	    #stSK.#hSchrittzeit(IN := TRUE,
	                        PT := T#24D_20H_31M_23S_647MS);
	END_REGION
	//----------------------------------------------------------------------------------------------------------------------------------
	REGION Schittkette Transport Staurollenketten
	    //=====================================================================
	    // Schrittkette Transport Staurollenkette
	    //=====================================================================
	    //----------------------------------------------------------------------------------------------------------------------------------
	    IF "DB_BA".stStatus.xIstSteuerungEin AND ("DB_BA".stBetriebsart.xAutomatikLaeuft OR "DB_BA".stBetriebsart.xGrundstellung_Laeuft OR "DB_BA".stBetriebsart.xEinrichten) THEN
	        //----------------------------------------------------------------------------------------------------------------------------------
	        CASE #stSK.#swSchrittnummer OF
	                //----------------------------------------------------------------------------------------------------------------------------------  
	            0:    // SK1 - INIT
	                //---------------------------------------------------------------------------------------------------------------------------------- 
	                #O_xMotor_Rück1 := FALSE;
	                #O_xMotor_Vor1 := FALSE;
	                #IO_stVisuHandTransport1.xRück := FALSE;
	                #IO_stVisuHandTransport1.xVor := FALSE;
	                #IO_stVisuHandTransport1.xStopp := FALSE;
	                
	                #O_xMotor_Rück2 := FALSE;
	                #O_xMotor_Vor2 := FALSE;
	                #IO_stVisuHandTransport2.xRück := FALSE;
	                #IO_stVisuHandTransport2.xVor := FALSE;
	                #IO_stVisuHandTransport2.xStopp := FALSE;
	                #stSK.#swSchrittnummer := 100;
	                //---------------------------------------------------------------------
	                
	                
	                // ##################################################################################################################################
	                
	                
	                //----------------------------------------------------------------------------------------------------------------------------------  
	            100:   // SK1 - Grundstellung
	                //---------------------------------------------------------------------------------------------------------------------------------- 
	                #bGeschwindigkeit1 := 0;
	                #IO_stVisuHandTransport1.xBandSteht := TRUE;
	                #bGeschwindigkeit2 := 0;
	                #IO_stVisuHandTransport2.xBandSteht := TRUE;
	                //---------------------------------------------------------------------
	                // Transport vor
	                // Automatischer Anlauf im Rechtslauf bei Staurollenketten
	                IF (TRUE) THEN
	                    #bGeschwindigkeit1 := #I_xKonfigMB.bSollGeschwindigkeit1;
	                    #bGeschwindigkeit2 := #I_xKonfigMB.bSollGeschwindigkeit2;
	                    #IO_stVisuHandTransport1.xBandSteht := FALSE;
	                    #IO_stVisuHandTransport2.xBandSteht := FALSE;
	                    #stSK.#swSchrittnummer := 150;
	                END_IF;
	                
	                
	                // ##################################################################################################################################
	                
	                
	                //----------------------------------------------------------------------------------------------------------------------------------   
	            150:   // SK1 - SEW Motor Rechtslauf
	                //----------------------------------------------------------------------------------------------------------------------------------  
	                // Umrechnung von Sekunden in Millisekunden für den Timer ( ZeitHMI * 1000)
	                IF (#stSK.hSchrittzeit.ET > DINT_TO_TIME(#I_xKonfigMB.swEinschaltverzoegerung * 1000)) THEN
	                    #O_xMotor_Freigabe1 := TRUE;
	                    #O_xMotor_Freigabe2 := TRUE;
	                    #stSK.#swSchrittnummer := 155;
	                END_IF;
	                //---------------------------------------------------------------------
	                
	                //----------------------------------------------------------------------------------------------------------------------------------   
	            155:   // SK1 - SEW Motor Rechtslauf
	                //---------------------------------------------------------------------------------------------------------------------------------- 
	                #bGeschwindigkeit1 := #I_xKonfigMB.bSollGeschwindigkeit1;
	                #O_xMotor_Vor1 := #xFreigabe1;
	                #bGeschwindigkeit2 := #I_xKonfigMB.bSollGeschwindigkeit2;
	                #O_xMotor_Vor2 := #xFreigabe2;
	                //---------------------------------------------------------------------
	                
	                
	                // ##################################################################################################################################
	                
	                
	                // Einrichten
	                //----------------------------------------------------------------------------------------------------------------------------------   
	            200:  // SK1 - SEW Motor Linkslauf / Rechtslauf / Stopp / Einrichten
	                //---------------------------------------------------------------------------------------------------------------------------------- 
	                #bGeschwindigkeit1 := #I_xKonfigMB.bSollGeschwindigkeit1;
	                #O_xMotor_Freigabe1 := TRUE;
	                #bGeschwindigkeit2 := #I_xKonfigMB.bSollGeschwindigkeit2;
	                #O_xMotor_Freigabe2 := TRUE;
	                //---------------------------------------------------------------------
	                //Motor Links
	                IF (#IO_stVisuHandTransport1.xRück = TRUE AND #O_xMotor_Vor1 = FALSE) THEN
	                    #O_xMotor_Rück1 := TRUE;
	                    #IO_stVisuHandTransport1.xRück := FALSE;
	                ELSE
	                    #IO_stVisuHandTransport1.xRück := FALSE;
	                END_IF;
	                //---------------------------------------------------------------------
	                //Motor Rechts
	                IF (#IO_stVisuHandTransport1.xVor = TRUE AND #O_xMotor_Rück1 = FALSE) THEN
	                    #O_xMotor_Vor1 := TRUE;
	                    #IO_stVisuHandTransport1.xVor := FALSE;
	                ELSE
	                    #IO_stVisuHandTransport1.xVor := FALSE;
	                END_IF;
	                //---------------------------------------------------------------------
	                //Motor Stopp
	                IF (#IO_stVisuHandTransport1.xStopp = TRUE) THEN
	                    #IO_stVisuHandTransport1.xStopp := FALSE;
	                    #IO_stVisuHandTransport1.xVor := FALSE;
	                    #IO_stVisuHandTransport1.xRück := FALSE;
	                    #O_xMotor_Rück1 := FALSE;
	                    #O_xMotor_Vor1 := FALSE;
	                END_IF;
	                // +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
	                // +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
	                IF (#I_xKonfigMB.xTypGetrennterTransport = TRUE) THEN
	                    //Motor Links
	                    IF (#IO_stVisuHandTransport2.xRück = TRUE AND #O_xMotor_Vor2 = FALSE) THEN
	                        #O_xMotor_Rück2 := TRUE;
	                        #IO_stVisuHandTransport2.xRück := FALSE;
	                    ELSE
	                        #IO_stVisuHandTransport2.xRück := FALSE;
	                    END_IF;
	                    //---------------------------------------------------------------------
	                    //Motor Rechts
	                    IF (#IO_stVisuHandTransport2.xVor = TRUE AND #O_xMotor_Rück2 = FALSE) THEN
	                        #O_xMotor_Vor2 := TRUE;
	                        #IO_stVisuHandTransport2.xVor := FALSE;
	                    ELSE
	                        #IO_stVisuHandTransport2.xVor := FALSE;
	                    END_IF;
	                    //---------------------------------------------------------------------
	                    //Motor Stopp
	                    IF (#IO_stVisuHandTransport2.xStopp = TRUE) THEN
	                        #IO_stVisuHandTransport2.xStopp := FALSE;
	                        #IO_stVisuHandTransport2.xVor := FALSE;
	                        #IO_stVisuHandTransport2.xRück := FALSE;
	                        #O_xMotor_Rück2 := FALSE;
	                        #O_xMotor_Vor2 := FALSE;
	                    END_IF;
	                END_IF;
	                //---------------------------------------------------------------------
	                //---------------------------------------------------------------------
	                // WEITERSCHALTBEDINGUNG
	                IF NOT "DB_BA".stBetriebsart.xEinrichten THEN
	                    #bGeschwindigkeit1 := 0;
	                    #O_xMotor_Rück1 := FALSE;
	                    #O_xMotor_Vor1 := FALSE;
	                    #O_xMotor_Freigabe1 := FALSE;
	                    #IO_stVisuHandTransport1.xRück := FALSE;
	                    #IO_stVisuHandTransport1.xVor := FALSE;
	                    #IO_stVisuHandTransport1.xStopp := FALSE;
	                    
	                    #bGeschwindigkeit2 := 0;
	                    #O_xMotor_Rück2 := FALSE;
	                    #O_xMotor_Vor2 := FALSE;
	                    #O_xMotor_Freigabe2 := FALSE;
	                    #IO_stVisuHandTransport2.xRück := FALSE;
	                    #IO_stVisuHandTransport2.xVor := FALSE;
	                    #IO_stVisuHandTransport2.xStopp := FALSE;
	                    
	                    #stSK.#swSchrittnummer := 0;
	                END_IF;
	                //---------------------------------------------------------------------
	                //---------------------------------------------------------------------
	        END_CASE;
	    ELSE
	        #O_xMotor_Rück1 := FALSE;
	        #O_xMotor_Vor1 := FALSE;
	        #O_xMotor_Freigabe1 := FALSE;
	        #IO_stVisuHandTransport1.xRück := FALSE;
	        #IO_stVisuHandTransport1.xVor := FALSE;
	        #IO_stVisuHandTransport1.xStopp := FALSE;
	        #O_xMotor_Rück2 := FALSE;
	        #O_xMotor_Vor2 := FALSE;
	        #O_xMotor_Freigabe2 := FALSE;
	        #IO_stVisuHandTransport2.xRück := FALSE;
	        #IO_stVisuHandTransport2.xVor := FALSE;
	        #IO_stVisuHandTransport2.xStopp := FALSE;
	        #stSK.#swSchrittnummer := 0;
	    END_IF;
	END_REGION
	//----------------------------------------------------------------------------------------------------------------------------------
	REGION Geschwindigkeit Transportband
	    //Motor Sollwertvorgabe Geschwindigkeit
	    // 32767 / 100 -> Auflösung scallieren
	    #O_slSollwert1 := ROUND(327.67 * #bGeschwindigkeit1);
	    #O_slSollwert2 := ROUND(327.67 * #bGeschwindigkeit2);
	END_REGION
	//----------------------------------------------------------------------------------------------------------------------------------
END_FUNCTION_BLOCK

